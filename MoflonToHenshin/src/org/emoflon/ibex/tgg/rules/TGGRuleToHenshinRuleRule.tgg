#using org.moflon.tggtomoflon.*
#using AttrCondDefLibrary.*

#rule TGGRuleToHenshinRuleRule #with MoflonToHenshin

#source { 
	tTgg : TGG{
		++ - rules -> tTggRule
	}
	
	++ tTggRule : TGGRule
}

#target {
	hModule : Module {
		++ - units -> hRule
	}
	
	++ hRule : Rule{
		++ - lhs -> lhs
		++ - rhs -> rhs
		++ - mappings -> mapping
	}
	
	++ lhs : Graph{
		name := "LHS"
	}
	
	++ rhs : Graph{
		name := "RHS"
	}
	
	++ mapping : Mapping
	
}

#correspondence {
  tggToModule : MoflonToHenshin.TGGToModule {
  	#src->tTgg
  	#trg->hModule
  }
  
  ++ tggRuleToHenshinRule : MoflonToHenshin.TGGRuleToHenshinRule {
  	#src->tTggRule
  	#trg->hRule
  }
}

#attributeConditions {
	eq_string(tTggRule.name, hRule.name)
}
